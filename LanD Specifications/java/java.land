DIRECTIVE	: '#' ~[\n\r]*

COMMENT		: COMMENT_L|COMMENT_ML
COMMENT_L	: '//' ~[\n\r]*
COMMENT_ML	: '/*' .*? '*/'

STRING		: STRING_STD
STRING_STD	: '"' ('\\"'|'\\\\'|.)*? '"'
STRING_INT_CODE : '{' (STRING|CHAR|STRING_INT_CODE|.)*? '}'

NEW_LINE	: '\n'
CHAR		: '\'' ('\\\''|'\\\\'|.)*? '\''
MODIFIER	: 'transient'|'strictfp'|'native'|'public'|'private'|'protected'|'static'|'const'|'new'|'final'|'synchronized'|'abstract'|'volatile'|'default'
ID			: [_$a-zA-Z][_$0-9a-zA-Z]*
 
 
curve_bracketed	= %left '{' %right '}'
round_bracketed	= %left '(' %right ')'
square_bracketed = %left '[' %right ']'

 
file_content	=	(AnyInclude('@interface')|outer_entity)*

outer_entity	=	
	annotation* MODIFIER*
	(
	 	'enum' name Any '{' Any '}' ';'? 	=> enum	|
		('class'|'interface') name Any '{' (inner_entity|AnyInclude('@interface'))* '}' ';'?	=> class_interface
	)
inner_entity	=	
	annotation* MODIFIER*
	(
	 	'enum' name Any '{' Any '}' ';'? 								=> inner_enum	|
		('class'|'interface') name Any '{' (inner_entity|Any)* '}' ';'?	=> inner_class_interface	|
		('<' Any '>')? %priority(2) type name
		(
	  		arguments Any (';' | block)									=> method	|
	  		('['']')* init_value? (',' name ('['']')* init_value?)* ';'	=> field
	  	)
	)
init_value	=	'=' (Any | %ghost type)+

// 1) AnyInclude('@interface') - пропускаем определения аннотаций
// 2) нас не интересуют local class-ы, которые могут быть внутри блока
// 3) блоки инициализации пропускаются за счёт пары {} и механизмов восстановления,
// 4) анонимные классы в инициализаторах пропускаются - блок включается в Any за счёт механизма пропуска пар 

name_atom	=	(ID | MODIFIER) type_parameter?
name		=	name_atom (('.'|'::') name_atom)*
names_list	=	name (',' name)*

type_atom	=	('unsigned'? ID) type_parameter? '*'*
type		=	type_atom ((('.'|'::') type_atom) | '['']')*!
type_parameter	=	'<' (AnyAvoid(';')|type_parameter)* '>'

arguments	=	'(' Any ')'
annotation	=	'@' name Any
block		=	'{' Any '}'

%%

%parsing recovery
%parsing fragment STRING_INT_CODE
%parsing start file_content
%parsing skip COMMENT STRING DIRECTIVE NEW_LINE

%nodes ghost  names_list
%nodes leaf name type

%mapping land enum inner_enum class_interface inner_class_interface method field
%mapping priority(3) name
 
DIRECTIVE	: '#' ~[\n\r]*

COMMENT		: COMMENT_L|COMMENT_ML
COMMENT_L	: '//' ~[\n\r]*
COMMENT_ML	: '/*' .*? '*/'

STRING		: STRING_STD
STRING_STD	: '"' ('\\"'|'\\\\'|.)*? '"'
STRING_INT_CODE : '{' (STRING|CHAR|STRING_INT_CODE|.)*? '}'

CHAR		: '\'' ('\\\''|'\\\\'|.)*? '\''
KEYWORD	: 'transient'|'strictfp'|'native'|'public'|'private'|'protected'|'static'|'const'|'new'|'final'|'synchronized'|'abstract'|'volatile'|'default'
ID			: [_$a-zA-Z][_$0-9a-zA-Z]*
 
file_content	=	entity*

entity	=	enum | class_interface | method | field | water_entity

enum			=	common_beginning 'enum' name Any block ';'?
class_interface	=	common_beginning ('class'|'interface') name Any '{' entity* '}' ';'?
method			=	common_beginning type_parameter? type name arguments Any (';' | block)	
field			=	common_beginning type_parameter? type name ('['']')* init_value? (',' name ('['']')* init_value?)* ';'
water_entity	=	(AnyInclude('@interface', 'import', 'package') | round_bracketed_water) (block | ';')+

common_beginning	=	(annotation|KEYWORD)*
init_value			=	'=' init_part+
init_part			=	water | type

name_atom	=	(ID | KEYWORD) type_parameter?
name		=	name_atom (('.'|'::') name_atom)*
names_list	=	name (',' name)*

type_atom		=	('unsigned'? ID) type_parameter? '*'*
type			=	type_atom ((('.'|'::') type_atom) | '['']')*!
type_parameter	=	'<' (AnyAvoid(';') | name)* '>'

arguments	=	'(' (Any|arguments)+ ')'
annotation	=	'@' name arguments?
block		=	'{' (Any|block)+ '}'

round_bracketed_water	=	'(' (Any|round_bracketed_water)+ ')'
curve_bracketed_water	=	'{' (Any|curve_bracketed_water)+ '}'
water					=	(Any | curve_bracketed_water | round_bracketed_water)+

%%

%parsing {
	recovery entity init_part
	fragment STRING_INT_CODE
	start file_content
	skip COMMENT STRING DIRECTIVE
}

%nodes {
	ghost names_list common_beginning entity
	leaf name type type_parameter init_value
}

%mapping {
	land enum class_interface method field
	priority(3) name
}
 
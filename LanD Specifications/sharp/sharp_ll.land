DIRECTIVE	: '#' ~[\n\r]*

COMMENT		: COMMENT_L|COMMENT_ML
COMMENT_L	: '//' ~[\n\r]*
COMMENT_ML	: '/*' .*? '*/'

CHAR		: '\'' ('\\\''|'\\\\'|.)*? '\''
MODIFIER	: 'ref'|'fixed'|'public'|'private'|'protected'|'internal'|'static'|'virtual'|'const'|'override'|'new'|'sealed'|'unsafe'|'readonly'|'abstract'|'volatile'|'async'|'partial'
ID			: '@'?[_a-zA-Z\u0430-\u044F\u0410-\u042F][_0-9a-zA-Z\u0430-\u044F\u0410-\u042F]*
GENERAL_ATTRIBUTE_START	:	'[' [ \t\r\f\n]*? ('assembly'|'module')
 
STRING_STD	: %left '"' %right '"' %inside ('\\"'|'\\\\')
STRING_VERB	: %left '@"' %right '"' %inside '""'
STRING_INT	: %left '$"' %right '"' %inside ('\\"'|'\\\\'|'{{'|STRING_INT_CODE)
STRING_VERB_INT	: %left '$@"' %right '"' %inside ('""'|'{{'|STRING_INT_CODE)
STRING_INT_CODE : %left '{' %right '}' %inside (STRING_STD|STRING_VERB|STRING_INT|STRING_VERB_INT|CHAR)

CURVE_BRACKETED : %left '{' %right '}'
ROUND_BRACKETED : %left '(' %right ')'
SQUARE_BRACKETED : %left ('['|GENERAL_ATTRIBUTE_START) %right ']'

 
namespace_content	=	opening_directive*! (namespace|entity|general_attribute)*
opening_directive	=	('using'|'extern') Any ';'
namespace			=	'namespace' name '{' namespace_content '}'

entity =
		entity_attribute* modifier*
		(
		 	'enum' name Any '{' Any '}' ';'?								=> enum	|
			%markup(priority(0.3), exactmatch) ('class'|'struct'|'interface') name Any '{' entity* '}' ';'?	=> class_struct_interface	|
			type name
			(
		  		arguments Any (init_expression? ';' | block)							=> method	|
		  		('[' Any ']')? init_value? (',' name ('[' Any ']')? init_value?)* ';'	=> field	|
		  		(block (init_value ';')? | init_expression ';')							=> property
		  	)
		) | AnyInclude('delegate', 'operator', 'this') (block | ';')+

modifier			=	MODIFIER | 'extern'
init_expression		=	'=>' Any
init_value			=	'=' init_part+
init_part			=	Any | type

name_atom	=	ID type_parameters?
name		=	name_atom (('.'|'::') name_atom)*
names_list	=	name (',' name)*

tuple		=	'(' type name? (',' type name?)* ')'
type_atom	=	('unsigned'? ID | tuple) type_parameters? '?'? '*'*
type		=	type_atom ((('.'|'::') type_atom) | ('[' Any ']'))*!
type_parameters	=	'<' (AnyAvoid(';') | type_parameters)* '>'

entity_attribute	=	'[' Any ']'
general_attribute	=	GENERAL_ATTRIBUTE_START Any ']'
arguments			=	'(' Any ')'
block				=	'{' Any '}'

%%

%parsing {
	recovery entity init_part
	start namespace_content
	skip COMMENT STRING_STD STRING_VERB STRING_INT STRING_VERB_INT DIRECTIVE
	nesting CURVE_BRACKETED ROUND_BRACKETED SQUARE_BRACKETED
	fragment STRING_INT_CODE
}

%nodes {
	ghost names_list
	leaf name type modifier arguments
}

%customblock {
	start("/// land start")
	end("/// land end")
	basetoken COMMENT
}

%markup {
	land namespace enum class_struct_interface method field property
	
	priority(0.1) modifier
	priority(0.8) name
	priority(0.3) type arguments
	priority(0) '{' '}' ';' ',' '=>' '=' 'namespace'
	
	exactmatch modifier
}
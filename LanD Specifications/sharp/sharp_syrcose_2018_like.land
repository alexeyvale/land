DIRECTIVE	: '#' ~[\n\r]*

COMMENT		: COMMENT_L|COMMENT_ML
COMMENT_L	: '//' ~[\n\r]*
COMMENT_ML	: '/*' .*? '*/'

STRING		: STRING_STD|STRING_VERB|STRING_INT
STRING_STD	: '"' ('\\"'|'\\\\'|.)*? '"'
STRING_VERB	: '@'('"' (~["]|'""')* '"')+
STRING_INT	: '$"' ('\\"'|'\\\\'|'{{'|STRING_INT_CODE|.)*? '"' | '$@' ('"' ('""'|'{{'|STRING_INT_CODE|.)*? '"')+
STRING_INT_CODE : '{' (STRING|CHAR|.)*? '}'

CHAR		: '\'' ('\\\''|'\\\\'|.)*? '\''
KEYWORD	: 'ref'|'fixed'|'public'|'private'|'protected'|'internal'|'static'|'virtual'|'const'|'override'|'new'|'sealed'|'unsafe'|'readonly'|'abstract'|'volatile'|'async'|'partial'
ID			: '@'?[_a-zA-Z\u0430-\u044F\u0410-\u042F][_0-9a-zA-Z\u0430-\u044F\u0410-\u042F]*
GENERAL_ATTRIBUTE_START	:	'[' [ \t\r\f\n]*? ('assembly'|'module')
 
CURVE_BRACKETED : %left '{' %right '}'
ROUND_BRACKETED : %left '(' %right ')'
SQUARE_BRACKETED : %left ('['|GENERAL_ATTRIBUTE_START) %right ']'

 
namespace_content	=	opening_directive*! (namespace|entity|general_attribute)*
opening_directive	=	('using'|'extern') Any ';'
namespace			=	'namespace' name '{' namespace_content '}'

entity		=	water_entity | entity_attribute* keyword* (enum_tail | class_tail | member_tail)

enum_tail	=	'enum' name Any '{' Any '}' ';'?
class_tail	=	('class'|'struct'|'interface') name Any '{' entity* '}' ';'?
member_tail	=	type name (method_tail | field_tail | property_tail)

method_tail	=	arguments Any (init_expression? ';' | block)
field_tail	=	('[' Any ']')? init_value? (',' name ('[' Any ']')? init_value?)* ';'
property_tail	=	(block (init_value ';')? | init_expression ';')

water_entity	=	AnyInclude('delegate', 'operator', 'this') (block | ';')+
		
keyword			=	KEYWORD | 'extern'
init_expression		=	'=>' Any
init_value			=	'=' init_part+
init_part			=	Any | type

name_atom	=	ID type_parameters?
name		=	name_atom (('.'|'::') name_atom)*
names_list	=	name (',' name)*

tuple		=	'(' type name? (',' type name?)* ')'
type_atom	=	('unsigned'? ID | tuple) type_parameters? '?'? '*'*
type		=	type_atom ((('.'|'::') type_atom) | ('[' Any ']'))*!
type_parameters	=	'<' (AnyAvoid(';') | type_parameters)* '>'

entity_attribute	=	'[' Any ']'
general_attribute	=	GENERAL_ATTRIBUTE_START Any ']'
arguments			=	'(' Any ')'
block				=	'{' Any '}'

%%

%parsing {
	ignoreundefined
	recovery entity init_part
	fragment STRING_INT_CODE
	start namespace_content
	skip COMMENT STRING DIRECTIVE
}

%nodes {
	ghost names_list
	leaf name type keyword init_value init_expression
}
DIRECTIVE	: '#' ~[\n\r]*

COMMENT		: COMMENT_L|COMMENT_ML
COMMENT_L	: '//' ~[\n\r]*
COMMENT_ML	: '/*' .*? '*/'

STRING		: STRING_STD|STRING_VERB|STRING_INT
STRING_STD	: '"' ('\\"'|'\\\\'|.)*? '"'
STRING_VERB	: '@'('"' (~["]|'""')* '"')+
STRING_INT	: '$"' ('\\"'|'\\\\'|'{{'|STRING_INT_CODE|.)*? ["\n\r]
STRING_INT_CODE : '{' (STRING|CHAR|STRING_INT_CODE|.)*? '}'

CHAR		: '\'' ('\\\''|'\\\\'|.)*? '\''
MODIFIER	: 'public'|'private'|'protected'|'internal'|'static'|'virtual'|'const'|'override'|'new'|'sealed'|'partial'|'unsafe'|'readonly'|'abstract'|'volatile'|'async'
ID			: '@'?[_a-zA-Z][_0-9a-zA-Z]* | '$'ID'$'
 
 
curve_bracketed	= %left '{' %right '}'
round_bracketed	= %left '(' %right ')'
square_bracketed = %left '[' %right ']'

 
namespace_content	=	opening_directive*! (namespace|entity)*
opening_directive	=	('using'|'extern') Any ';'
namespace			=	'namespace' name '{' namespace_content '}'

entity =
		(MODIFIER|'extern')*
		(
		 	'enum' name Any '{' Any '}' ';'? 								=> enum	|
			('class'|'struct'|'interface') name Any '{' entity* '}' ';'?	=> class_struct_interface	|
			%priority(2) type %priority(3) name
			(
		  		arguments Any (init_expression? ';' | block)	=> method	|
		  		init_value? (',' name init_value?)* ';'			=> field	|
		  		(block (init_value ';')? | init_expression ';')	=> property
		  	)
		) | AnyInclude('operator')

init_expression		=	'=>' (Any | %ghost type)+
init_value			=	'=' (Any | %ghost type)+

name_atom	=	(ID | MODIFIER) ('<' types_list '>')?
name		=	name_atom (('.'|'::') name_atom)*
names_list	=	name (',' name)*

tuple		=	'(' type name? (',' type name?)* ')'
type_atom	=	('unsigned'? ID | tuple) ('<' types_list '>')? '?'? '*'*
type		=	type_atom ((('.'|'::') type_atom) | ('[' ','* ']'))*!
types_list	=	(attribute|'in'|'out')* type? (',' (attribute|'in'|'out')* type?)*

arguments	=	'(' Any ')'
attribute	=	'[' Any ']'
block		=	'{' Any '}'

%%

%parsing fragment STRING_INT_CODE
%parsing start namespace_content
%parsing skip COMMENT STRING DIRECTIVE

%nodes ghost  names_list
%nodes leaf name type

%mapping land namespace enum class_struct_interface method field property
 
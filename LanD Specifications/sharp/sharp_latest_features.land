DIRECTIVE	: '#' ~[\n\r]*

COMMENT		: COMMENT_L|COMMENT_ML
COMMENT_L	: '//' ~[\n\r]*
COMMENT_ML	: '/*' .*? '*/'

STRING		: STRING_STD|STRING_VERB|STRING_INT
STRING_STD	: '"' ('\\"'|'\\\\'|.)*? '"'
STRING_VERB	: '@'('"' (~["]|'""')* '"')+
STRING_INT	: '$"' ('\\"'|'\\\\'|'{{'|STRING_INT_CODE|.)*? '"' | '$@' ('"' ('""'|'{{'|STRING_INT_CODE|.)*? '"')+
STRING_INT_CODE : '{' (STRING|CHAR|.)*? '}'

CHAR		: '\'' ('\\\''|'\\\\'|.)*? '\''
MODIFIER	: 'ref'|'fixed'|'public'|'private'|'protected'|'internal'|'static'|'virtual'|'const'|'override'|'new'|'sealed'|'unsafe'|'readonly'|'abstract'|'volatile'
ID			: '@'?[_a-zA-Z\u0430-\u044F\u0410-\u042F][_0-9a-zA-Z\u0430-\u044F\u0410-\u042F]*
GENERAL_ATTRIBUTE_START	:	'[' [ \t\r\f\n]*? ('assembly'|'module')
 
CURVE_BRACKETED : %left '{' %right '}'
ROUND_BRACKETED : %left '(' %right ')'
SQUARE_BRACKETED : %left ('['|GENERAL_ATTRIBUTE_START) %right ']'

 
namespace_content	=	opening_directive*! (namespace|entity|general_attribute)*
opening_directive	=	('using'|'extern') Any ';'
namespace			=	'namespace' name '{' namespace_content '}'

entity =
		entity_attribute* (MODIFIER|'extern'|'async'|'partial')*
		(
		 	'enum' name Any '{' Any '}' ';'?								=> enum	|
			('class'|'struct'|'interface') name Any '{' entity* '}' ';'?	=> class_struct_interface	|
			%priority(2) type name
			(
		  		arguments Any (init_expression? ';' | block)							=> method	|
		  		('[' Any ']')? init_value? (',' name ('[' Any ']')? init_value?)* ';'	=> field	|
		  		(block (init_value ';')? | init_expression ';')							=> property
		  	)
		) | AnyInclude('delegate', 'operator', 'this') (block | ';')+

init_expression		=	'=>' (Any | %ghost type)+
init_value			=	'=' (Any | %ghost type)+

name_atom	=	('async'|'partial'|ID) type_parameters?
name		=	name_atom (('.'|'::') name_atom)*
names_list	=	name (',' name)*

tuple		=	'(' type name? (',' type name?)* ')'
type_atom	=	('unsigned'? ID | tuple) type_parameters? '?'? '*'*
type		=	type_atom ((('.'|'::') type_atom) | ('[' ','* ']'))*!
type_parameters	=	'<' (AnyAvoid(';')|type_parameters)* '>'

entity_attribute	=	'[' Any ']'
general_attribute	=	GENERAL_ATTRIBUTE_START Any ']'
arguments			=	'(' Any ')'
block				=	'{' Any '}'

%%

%parsing {
	recovery
	fragment STRING_INT_CODE
	start namespace_content
	skip COMMENT STRING DIRECTIVE
}

%nodes {
	ghost names_list
	leaf name type
}

%mapping {
	land namespace enum class_struct_interface method field property
	priority(5) name
	exactmatch MODIFIER
}